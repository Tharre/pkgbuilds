# Maintainer: Tharre <tharre3@gmail.com>

pkgbase=arch-system
pkgname=('base-system' 'backup-system' 'xps13')
pkgver=$(printf '%(%Y.%m.%d)T')
pkgrel=1
pkgdesc="Arch Linux system configuration"
arch=("any")
url=""
license=('custom')
source=('etc-pacman.conf' 'etc-locale.conf' 'etc-locale.gen' 'etc-vconsole.conf'
        'mirrorupgrade.hook' 'sudoers-wheel' 'etc-nsswitch.conf'
        'etc-sysctl.conf' '90-modprobe-d.hook' 'etc-yubikey.rules'

        'sudoers-borgbackup' 'backup-sudo' 'borg-backup.service'
        'borg-backup.timer' 'borgbackup'

        'intel-undervolt.conf' 'xps13-i915.conf')
sha256sums=('271d6a7ac30da0d2ba055067d4ebbab76812c1735e4c2b45c619e004edc1fbba'
            '70b74b741f3b1b58eeb97d8d5b9e6148a35ff8dced47cf4a231c1d0420aff5eb'
            'b11ab0ea3d5c300afa0fb877d1f367993425a3fda0382f50f0e122625154f566'
            '978271534da06356159eb24c0cee32b0024029d41df836ab76a964dfd7d36447'
            '79ba5fdf0c69686ea0f2770c9b49b58ca26ebcce15e346d5bd56a442e684896e'
            '62c1a085f7a9d1c5d57b40516fea5d7e8dfb3593d5c0a713cd571c8e5bae2584'
            '9c1648e1e9219efbe1e4098952f7595f1ebb0eca629d2c8957079186c56d1822'
            '67b6ebe8a8e52cd60266f418b7af77c2ef0b2b6596de494ec5c37824f355a3ae'
            '7467bb40e7d404ed249b34a0e410b8d452ebc4b90588d7e210bfa47011b5981d'
            'dd84877b7f6f4330da489fa33f843c9a7c862562a3d78dc2feea5116ac846a79'
            '483a7d9350653c2cf22f36f67f1a83cc5c95232dfc10642a20d76e3c32c9e2e3'
            'd39732bec90036020ad94d52cb34b6215801a82595f7fb30f8c5e0a7f1eb9b1e'
            '94c06cbf7304996b0503cace9394c2dbc0150bc045fd30cc402e3d943a8c8abd'
            'e1bf3dc5978a7641f86975b7ee770a6914b30a8e4b7e3be2f1b07eaa7098591d'
            '276a79a04359ffae6ddf597d060278f6f58890ce2d5f6c185948b31c7a5c8927'
            '895305808aa91be3294dbaeb7840e38497fb8dbcd6309cc95c417e1a0379e596'
            '5613c98d32e513a973e8a0e250ab0b267882a73615f507b6208d4a5f21487fe6')

package_base-system() {
    depends=('gvim' 'zsh' 'tmux' 'htop' 'git' 'reflector' 'sudo' 'avahi'
             'nss-mdns' 'ttf-dejavu' 'chromium' 'ttf-liberation')
    install="$pkgname.install"

    install -Dm0644 etc-pacman.conf "$pkgdir"/etc/pacman.conf
    install -Dm0644 etc-locale.conf "$pkgdir"/etc/locale.conf
    install -Dm0644 etc-locale.gen "$pkgdir"/etc/locale.gen
    install -Dm0644 etc-vconsole.conf "$pkgdir"/etc/vconsole.conf
    install -Dm0644 mirrorupgrade.hook -t "$pkgdir"/usr/share/libalpm/hooks/
    install -dm0750 "$pkgdir"/etc/sudoers.d/
    install -Dm0644 sudoers-wheel "$pkgdir"/etc/sudoers.d/00_wheel
    install -Dm0644 etc-nsswitch.conf "$pkgdir"/etc/nsswitch.conf
    install -Dm0644 etc-sysctl.conf "$pkgdir"/usr/lib/sysctl.d/99-sysctl.conf
    install -Dm0644 90-modprobe-d.hook -t "$pkgdir"/usr/share/libalpm/hooks/
    install -Dm0644 etc-yubikey.rules -t "$pkgdir"/usr/lib/udev/rules.d

    ln -s /usr/share/zoneinfo/Europe/Berlin "$pkgdir"/etc/localtime
}

package_backup-system() {
    depends=('libnotify' 'bash' 'borg' 'sudo')

    install -dm0750 "$pkgdir"/etc/sudoers.d/
    install -Dm0644 sudoers-borgbackup "$pkgdir"/etc/sudoers.d/70_borgbackup
    install -Dm0644 borg-backup.service -t "$pkgdir"/usr/lib/systemd/user/
    install -Dm0644 borg-backup.timer -t "$pkgdir"/usr/lib/systemd/user/
    install -Dm0755 borgbackup backup-sudo -t "$pkgdir"/usr/bin/
}

package_xps13() {
    depends=('mesa' 'lib32-mesa' 'vulkan-intel' 'libva-intel-driver'
             'lib32-libva-intel-driver' 'libvdpau-va-gl' 'intel-undervolt')

    install -Dm0644 xps13-i915.conf "$pkgdir"/etc/modprobe.d/i915.conf
    install -Dm0644 intel-undervolt.conf -t "$pkgdir"/etc/
}

# TODO: docker, vagrant, devtools
